#!/usr/bin/env perl
use strict;
use warnings;

# Examples:
# perl cpan-avatars 10      # A 10x10 collage starting at author 1
# perl cpan-avatars 3 1000  # A 3x3 collage starting at author 1000
# perl cpan-avatars 2 GENE  # A 2x2 collage starting at author GENE
# perl cpan-avatars 1 1 1   # Dump out the author list and exit

use Acme::CPANAuthors;
use File::Slurper 'write_text';
use HTTP::Simple;
use Imager;
use List::SomeUtils 'first_index';
use Mojo::DOM;

my $side  = shift || 3; # Number of images per side of the collage
my $start = shift || 1; # Where in the alphabetical order to begin the collage - number or author name
my $dump  = shift || 0; # Optional flag to dump the author list and exit

my $path  = $ENV{HOME} . '/tmp/avatars/'; # Where our transient avatars and the collage live
my $width = 80; # Avatar side pixel size
my $size  = $width * $side; # The width of the collage
my $max   = $side ** 2; # Number of avatars to fetch

# Fetch the author modules
my $url = 'https://www.cpan.org/modules/01modules.index.html';
my $content = get $url;
print "Fetched $url\n";

# Parse the author modules text into lines
my $dom = Mojo::DOM->new($content);
my @lines = split /\n/, $dom->at('pre')->all_text;

my %authors = (); # Bucket for the authors and their avatars

# Parse the author module lines
for my $line (@lines) {
    # module author etc.
    my @parts = $line =~ /^(\w+)\s+([A-Z]+)\s+.*$/;
    next unless $parts[0];
    $authors{ $parts[1] }++;
}

my $i = 0; # Generic loop counter and author index

# Inspect the indices of the authors
if ($dump) {
    for my $author (sort keys %authors) {
        $i++;
        print "$i. $author: $authors{$author}\n";
    }
    exit;
}

if ($start =~ /^[A-Za-z]+$/) {
    $start = first_index { CORE::fc($_) eq CORE::fc($start) } sort keys %authors;
    $start++;
}

# Remove authors that precede the given start
for my $author (sort keys %authors) {
    $i++;
    if ($i < $start) {
        delete $authors{$author};
    }
    else {
        last;
    }
}

my $authors = Acme::CPANAuthors->new;

$i = 0;

# Fetch the author avatars
for my $author (sort keys %authors) {
    $i++;

    $authors{$author} = $authors->avatar_url($author);

    my $file = $path . $author;

    my $status = getstore($authors{$author}, $file)
        or warn "Can't getstore for $file: $!\n";
    if ($status == 200) {
        print "$i. Saved $file\n";
    }
    else {
        print "WARNING: Could not save $file\n";
    }

    sleep 4; # Play nice

    last if $i >= $max;
}

# Build an HTML image map
my $html =<<'HTML';
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
        "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
  <meta http-equiv="Content-type" content="text/html; charset=utf-8" />
  <title>CPAN Author Collage</title>
</head>
<body>
<img src="collage.jpg" alt="CPAN Author Collage" usemap="#collage">
<map name="collage">
HTML

# Instantiate a blank collage image
my $collage = Imager->new(xsize => $size, ysize => $size)
    or die "Can't create image: ", Imager->errstr;

$i = 0;
my $j = 0; # x coordinate iterator
my $k = 0; # y coordinate iterator

# Read the author files and paste them onto the collage
for my $author (sort keys %authors) {
    my $file = $path . $author;

    my $img = Imager->new;
    $img->read(file => $file)
        or die "Can't read $file: ", $img->errstr;

    if ($j >= $side) {
        $j = 0;
        $k++;
    }
    my $x0 = $j * $width;
    my $x1 = $x0 + $width;
    my $y0 = $k * $width;
    my $y1 = $y0 + $width;
#    print "[$i,$j,$k] [$x,$y] [$x1,$y1]\n";

    $html .= qq|<area shape="rect" coords="$x0,$y0,$x1,$y1" alt="$author" href="https://metacpan.org/author/$author">\n|;

    $collage->paste(left => $x0, top => $y0, img => $img);

    unlink $file;

    $i++;
    $j++;

    last if $i >= $max;
}

# Save the collage
my $file = $path . 'collage.jpg';
$collage->write(file => $file) or
    die "Can't write to $file: ", $collage->errstr;
print "Saved $file\n";

# Finish the HTML image map
$html .=<<'HTML';
</map>
</body>
</html>
HTML

$file = $path . 'collage.html';
write_text($file, $html);
print "Saved $file\n";
