#!/usr/bin/env perl
use strict;
use warnings;

=head1 SYNOPSIS

Construct a collage of CPAN author gravatars.

Writeup: L<https://ology.github.io/2022/01/09/an-image-collage-of-cpan-authors/>

  # Examples:
  perl cpan-avatars 10      # A 10x10 collage starting at author 0
  perl cpan-avatars 2 1000  # A 2x2 collage starting at author 1000
  perl cpan-avatars 3 gene  # A 3x3 collage starting at author GENE
  perl cpan-avatars 4 -1    # A 4x4 collage of random authors

* If you CTRL-C out of a run, you will have to manually clean-up the
avatars directory. If there are any author files in the avatars
directory, they will possibly be mistakenly used in the generated
collage.

=cut

use File::Slurper 'write_text';
use HTTP::Simple;
use Imager;
use List::SomeUtils 'first_index';
use List::Util 'shuffle';
use Mojo::DOM;
use Mojo::UserAgent;
use Parse::CPAN::Authors;

my $side  = shift || 3; # Number of images per side of the collage
my $start = shift // 0; # Where to begin the collage - index number or author name
my $path  = shift || $ENV{HOME} . '/tmp/avatars/'; # Where our avatars and the collage live

my $width = 130;            # Fixed avatar side pixel size
my $size  = $width * $side; # The width of the collage
my $max   = $side ** 2;     # Number of avatars to fetch

my $base = 'https://metacpan.org/author/';
my $cpan = 'http://www.cpan.org/authors/';
my $file = '01mailrc.txt.gz';

my %authors;

die "Path '$path' does not exist"
    unless -d $path;

# Save the cpan authors file
get_file($cpan . $file, $file, "Saved $file");

my $pca = Parse::CPAN::Authors->new($file);

# Get each author metacpan url
for my $author ($pca->authors) {
    $authors{ $author->pauseid } = $base . $author->pauseid;
}

# Remove the cpan author file
unlink $file;

# Find the index of the given author name
if ($start ne '-1' && $start =~ /[A-Za-z]/) {
    my $author = $start;
    $start = first_index { CORE::fc($_) eq CORE::fc($start) } sort keys %authors;
    die "Author '$author' not found\n"
        if $start < 0;
}

if ($start > -1) {
    # Remove authors that precede the given start
    my $i = 0;
    for my $author (sort keys %authors) {
        if ($i < $start) {
            delete $authors{$author};
        }
        else {
            last;
        }
        $i++;
    }
}

die "No authors\n" unless keys %authors;

my $i = 0;

# Fetch the author avatar images
for my $author ($start == -1 ? shuffle keys %authors : sort keys %authors) {
    $i++;
    sleep 4 unless $i == 1; # play nice

    my $ua = Mojo::UserAgent->new;
    my $tx = $ua->get($authors{$author});
    my $dom = Mojo::DOM->new($tx->res->body);
    my $img = $dom->find('img[alt="Author image"]')->[0]->attr('src');
    my $img_file = $path . $author;
    get_file($img, $img_file, "$i. Saved $img_file");

    last if $i >= $max;
}

# Build an HTML image map
my $html =<<'HTML';
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
        "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
  <meta http-equiv="Content-type" content="text/html; charset=utf-8" />
  <title>CPAN Author Collage</title>
</head>
<body>
<img src="collage.jpg" alt="CPAN Author Collage" usemap="#collage">
<map name="collage">
HTML

# Instantiate a blank, square collage image
my $collage = Imager->new(xsize => $size, ysize => $size)
    or die "Can't create image: ", Imager->errstr;

$i = 0; # reset the incrementer
my $x = 0;
my $y = 0;

# Read the author files and paste them onto the collage
for my $author ($start == -1 ? keys %authors : sort keys %authors) {
    my $img_file = $path . $author;
    next unless -e $img_file;

    my $img = Imager->new;
    $img->read(file => $img_file)
        or die "Can't read $img_file: ", $img->errstr;

    unlink $img_file;

    # Compute the next coordinates
    if ($x >= $side) {
        $x = 0;
        $y++;
    }
    my $x0 = $x * $width;
    my $x1 = $x0 + $width;
    my $y0 = $y * $width;
    my $y1 = $y0 + $width;
#    print "[$i,$x,$y] [$x0,$y0] [$x1,$y1]\n";

    $html .= qq|<area shape="rect" coords="$x0,$y0,$x1,$y1" alt="$author" href="https://metacpan.org/author/$author" title="$author">\n|;

    $collage->paste(left => $x0, top => $y0, img => $img);

    $i++;
    $x++;

    last if $i >= $max;
}

# Save the collage image
$file = $path . 'collage.jpg';
$collage->write(file => $file) or
    die "Can't write to $file: ", $collage->errstr;
print "Saved $file\n";

# Finish the HTML image map
$html .=<<'HTML';
</map>
</body>
</html>
HTML

# Save the collage html
$file = $path . 'collage.html';
write_text($file, $html);
print "Saved $file\n";

sub get_file {
    my ($url, $file, $msg) = @_;
    my $status = getstore($url, $file)
        or die "Can't getstore $url to $file: $!\n";
    if ($status == 200) {
        print "$msg\n";
    }
    else {
        die "Could not get $file from $url\n";
    }
}
