#!/usr/bin/env perl
use strict;
use warnings;

use Acme::CPANAuthors;
use HTTP::Simple;
use HTML::TreeBuilder;
use Imager;

my $per_side = shift || 3; # Number of images per side of the collage

# Crucial parameters
my $path  = $ENV{HOME} . '/tmp/avatars/';
my $width = 80; # Avatar side pixel size
my $size  = $width * $per_side;
my $max   = $per_side ** 2; # Number of avatars to fetch

# Fetch the author modules
my $url = 'https://www.cpan.org/modules/01modules.index.html';
my $content = get $url;
print "Fetched $url\n";

# Parse the content into a tree
my $tree = HTML::TreeBuilder->new();
$tree->parse($content);
$tree->eof;

# Accumulate the text bits as lines
my $text = '';
for my $pre ( $tree->look_down( _tag => 'pre' ) ) {
    $text .= $pre->as_text;
}
my @lines = split /\n/, $text;

# Bucket for the authors and their avatars
my %authors = ();

for my $line ( @lines ) {
    # module, author, distribution, size, uploaded
    my @parts = $line =~ /^(\w+)\s+([A-Z]+)\s+(\S+)\s+(\d+\w)\s+(.*?)\s*\+?\s*$/g;
    next unless $parts[0];
    $authors{ $parts[1] }++;
}

my $authors = Acme::CPANAuthors->new;

my $i = 0;

# Fetch the author avatars
for my $author ( sort keys %authors ) {
    $i++;
    $authors{$author} = $authors->avatar_url($author);
    my $file = $path . "$author.jpg";
    my $status = getstore( $authors{$author}, $file );
    print "$i. Saved $file\n";
    sleep 4;
    last if $i >= $max;
}

my $collage = Imager->new(xsize => $size, ysize => $size)
    or die Imager->errstr;

$i = 0;
my $j = 0;
my $k = 0;

for my $author ( sort keys %authors ) {
    my $file = $path . "$author.jpg";
    my $img = Imager->new;
    $img->read(file => $file, type => 'jpeg')
        or die "Can't read $file: ", $img->errstr;
    if ($j >= $per_side) {
        $j = 0;
        $k++;
    }
    my $x = $j * $width;
    my $y = ($k % $per_side) * $width;
#    print "[$i,$j,$k] [$x,$y]\n";
    $collage->paste(left => $x, top => $y, img => $img);
    $i++;
    $j++;
    last if $i >= $max;
}

my $file = $path . 'collage.jpg';
$collage->write(file => $file) or
      die "Can't write to $file: ", $collage->errstr;
print "Saved $file\n";
