#!/usr/bin/env perl
use strict;
use warnings;

use HTML::TreeBuilder 5 -weak; # Ensure weak references in use
use List::Util qw/any/;
use HTTP::Simple;

my $include = shift || 'Math';
my $exclude = shift // '';

$include = [ split /,/, $include ];
$exclude = [ split /,/, $exclude ];

my $url = 'https://www.cpan.org/modules/01modules.index.html';
my $content = get $url;

my $tree = HTML::TreeBuilder->new();
$tree->parse($content);
$tree->eof;

my $text = '';
for my $pre ( $tree->look_down( _tag => 'pre' ) ) {
    $text .= $pre->as_text;
}

my $i = 0;

my @lines = split /\n/, $text;
for my $line ( @lines ) {
    next unless any { $line =~ /$_/ } @$include;
    next if any { $line =~ /$_/ } @$exclude;
    $i++;
    print "$i. $line\n";
}

print "\nTotal matches: $i\n";
