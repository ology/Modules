#!/usr/bin/env perl
use strict;
use warnings;

# Search CPAN distributions
#
# Example:
# $ perl parse-cpan Math
# $ perl parse-cpan Music,MIDI
# $ perl parse-cpan ''        # Show *every* distro!
# $ perl parse-cpan \\bAI\\b  # Regular expression matching AI distributions
# $ perl parse-cpan Math Geo  # Distros with Math AND Geo in the name
# $ perl parse-cpan Math Geo,Planar

use List::Util qw(all any);
use MetaCPAN::Client;
use Parse::CPAN::Packages::Fast;

STDOUT->autoflush;

my $or_query = shift // 'Music,MIDI';
$or_query = [ split /,/, $or_query ];

my $and_query = shift // '';
$and_query = [ split /,/, $and_query ];

my $file = $ENV{HOME} . '/.cpanm/sources/http%www.cpan.org/02packages.details.txt.gz';

print "Parsing $file ... ";
my $p = Parse::CPAN::Packages::Fast->new($file);
print "Done.\n";
print "Processing...\n";

my $mcpan = MetaCPAN::Client->new;

my $i = 0;

for my $item ($p->distributions) {
    my $name = $item->dist;
    next unless $name;
    next if @$or_query && !(any { $name =~ /$_/ } @$or_query);
    next if @$and_query && !(all { $name =~ /$_/ } @$and_query);
    $i++;
    my $release = eval { $mcpan->release($name) };
    my $date = eval { $release->date } || '?';
    my $url = "https://metacpan.org/dist/$name";
    printf "%d. %s %s %s %s\n", $i, $url, $item->version, $date, $item->cpanid;
}
