#!/usr/bin/env perl
use strict;
use warnings;

use List::Util qw(any);
use MetaCPAN::Client;
use Parse::CPAN::Packages::Fast;

STDOUT->autoflush;

my $query = shift || 'Music,MIDI';
$query = [ split /,/, $query ];

my $file = $ENV{HOME} . '/.cpanm/sources/http%www.cpan.org/02packages.details.txt.gz';

print "Parsing $file ... ";
my $p = Parse::CPAN::Packages::Fast->new($file);
print "Done.\n";
print "Processing...\n";

my $mcpan = MetaCPAN::Client->new;

my $i = 0;

for my $item ($p->distributions) {
    my $name = $item->dist;
    next unless $name && any { $name =~ /$_/ } @$query;
    $i++;
    my $release = eval { $mcpan->release($name) };
    my $date = eval { $release->date } || '?';
    my $url = "https://metacpan.org/dist/$name";
    printf "%d. %s %s %s (%s)\n", $i, $url, $item->version, $date, $item->cpanid;
}
