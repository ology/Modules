#!/usr/bin/env perl
use strict;
use warnings;

use HTML::TreeBuilder 5 -weak; # Ensure weak references in use
use WWW::Mechanize;

my $string = shift || 'Math';
my $extra  = shift || '';

my $url = 'https://www.cpan.org/modules/01modules.index.html';
my $mech = WWW::Mechanize->new();
$mech->get($url);

my $tree = HTML::TreeBuilder->new();
$tree->parse( $mech->content );
$tree->eof;

my $text = '';
for my $pre ( $tree->look_down( _tag => 'pre' ) ) {
    $text .= $pre->as_text;
}

my $i = 0;

my @lines = split /\n/, $text;
for my $line ( @lines ) {
    next unless $line =~ /$string/;
    next unless $line =~ /$extra/;
    print "$line\n";
    $i++;
}

print "\nTotal matches: $i\n";
